<?xml version="1.0" encoding="UTF-8" ?>
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:dss="http://dss.esig.europa.eu/validation/simple-certificate-report">
                
	<xsl:output method="html" encoding="utf-8" indent="yes" omit-xml-declaration="yes" />

    <xsl:template match="/dss:SimpleCertificateReport">
		<xsl:comment>Generated by DSS v.${project.version}</xsl:comment>
	    
		<xsl:apply-templates select="dss:Chain"/>
    </xsl:template>
    
	<xsl:template match="dss:Chain">
    	<xsl:apply-templates/>
    </xsl:template>

    <xsl:template match="dss:ChainItem">
		<div>
    		<xsl:attribute name="class">panel panel-primary</xsl:attribute>
    		<div>
    			<xsl:attribute name="class">panel-heading</xsl:attribute>
	    		<xsl:attribute name="data-target">#collapseCert-<xsl:value-of select="dss:id"/></xsl:attribute>
		       	<xsl:attribute name="data-toggle">collapse</xsl:attribute>
    			Certificate : 
	        </div>
    		<div>
    			<xsl:attribute name="class">panel-body collapse in</xsl:attribute>
	        	<xsl:attribute name="id">collapseCert-<xsl:value-of select="dss:id"/></xsl:attribute>
	        	
	        	<dl>
	        		<xsl:attribute name="class">dl-horizontal</xsl:attribute>
	        		
	        		<dt>Subject</dt>
	        		<dd><xsl:value-of select="dss:subject"/></dd>
	        		<dt>Not Before</dt>
	        		<dd><xsl:value-of select="dss:notBefore"/></dd>
	        		<dt>Not After</dt>
	        		<dd><xsl:value-of select="dss:notAfter"/></dd>
	        		
       				<dt>Revocation</dt>
	        		<xsl:choose>
	        			<xsl:when test="dss:revocation">
	        				<dd>Revoked (reason:<xsl:value-of select="dss:revocation/dss:revocationReason" /> @ <xsl:value-of select="dss:revocation/dss:revocationDate" />)</dd>
	        			</xsl:when>
	        			<xsl:otherwise>
	        				<dd>Not Revoked</dd>
	        			</xsl:otherwise>
	        		</xsl:choose>
	        		
	        		<xsl:if test="dss:ocspUrls or dss:crlUrls">
	        			<dt>Revocation Access</dt>
						<xsl:apply-templates select="dss:ocspUrls"/>
						<xsl:apply-templates select="dss:crlUrls"/>
	        		</xsl:if>
	        		
	        		<xsl:if test="dss:qualificationAtIssuance">
		        		<dt>Qualification at issuance time</dt>
		        		<dd><xsl:value-of select="dss:qualificationAtIssuance"/></dd>
	        		</xsl:if>
	        		<xsl:if test="dss:qualificationAtValidation">
		        		<dt>Qualification at validation time</dt>
		        		<dd><xsl:value-of select="dss:qualificationAtValidation"/></dd>
	        		</xsl:if>
	        		
	        	</dl>
    		</div>
    	</div>
    </xsl:template>
    
    <xsl:template match="dss:ocspUrls">
  		<dd>OCSP
	  		<ul>
				<xsl:apply-templates select="dss:url"/>
			</ul>
		</dd>
	</xsl:template>
  	<xsl:template match="dss:crlUrls">
  		<dd>CRL
			<ul>
				<xsl:apply-templates select="dss:url"/>
			</ul>
		</dd>
	</xsl:template>
    
    <xsl:template match="dss:url">
    	<li><a><xsl:attribute name="href"><xsl:value-of select="." /></xsl:attribute><xsl:value-of select="." /></a></li>
    </xsl:template>

</xsl:stylesheet>
